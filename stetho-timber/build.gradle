apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'digital.wup.android-maven-publish'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
    }

    lintOptions {
        warningsAsErrors true
        informational 'GradleDependency'
    }

    libraryVariants.all {
        it.generateBuildConfig.enabled = false
    }
}

dependencies {
    implementation "com.facebook.stetho:stetho:$rootProject.ext.stethoVersion"
    implementation "com.jakewharton.timber:timber:$rootProject.ext.timberVersion"
    compileOnly "org.jetbrains:annotations:$rootProject.ext.annotationsVersion"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.android

            artifact sourcesJar
        }
    }
}

bintray {
    user = findProperty('BINTRAY_USER')
    key = findProperty('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = 'stetho-timber'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/technoir42/stetho-timber.git'
        version {
            name = project.version
        }
        publications = ['maven']
    }
}
